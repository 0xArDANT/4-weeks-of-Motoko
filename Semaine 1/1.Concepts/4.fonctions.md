Credit à "The DAO Adventure"

⚙️ Fonctions 

Cette section se concentre uniquement sur les fonctions définies dans le corps d'un acteur à l'aide de la syntaxe {} de l'acteur. Toute fonction qui se trouve en dehors de la syntaxe de l'acteur {} sera traitée dans la suite. Un exemple simple Pour introduire des fonctions, prenons un exemple : voici un acteur qui est chargé de garder une trace d'un compteur.

actor Counter {
    var count : Nat = 0;

    public func setCount(n : Nat) : async Nat {
        count := n;
        return count;
    };

};

Le mot-clé func est utilisé pour déclarer les fonctions, suivi du nom donné à la fonction, dans ce cas increaseCount. En ce qui concerne les variables, la convention syntaxique est d'utiliser lowerCamelCase pour les noms de fonctions. 

# Typage de fonction 

Lors de la définition d'une fonction dans Motoko, les arguments typés et le type de retour sont utilisés pour déterminer le type de la fonction dans son ensemble. Par exemple, la fonction setCount est de type suivant :


setCount : (n : Nat) -> async Nat;

Pour déclarer une fonction dans Motoko, vous devez spécifier les types d'arguments et les valeurs de retour. Les arguments de la fonction sont placés entre parenthèses (), et dans ce cas, la fonction prend un argument n de type Nat. Après les arguments de la fonction, le type de retour est spécifié : async Nat.

Vous vous demandez peut-être pourquoi le type de retour de la fonction setCount est async Nat au lieu de simplement Nat? Le terme async signifie asynchrone, qui indique que dans le modèle d'acteur dont nous avons parlé précédemment, les canisters ou les acteurs communiquent entre eux de manière asynchrone. Lorsqu'un canister envoie une requête à un autre (ou lorsqu'un utilisateur appelle une fonction), il y a une brève période d'attente avant que l'appelant ne reçoive une réponse.

La programmation asynchrone vous permet d'exécuter votre code de manière non bloquante. Le type de retour async Nat de la fonction setCount indique que l'appelant doit attendre quelques instants avant de recevoir la valeur de retour. Finalement, la réponse sera une valeur de type Nat, mais avec un délai dû à la nature asynchrone de la communication entre les canisters. Toutes les fonctions publiques déclarées dans le corps d'un acteur doivent avoir le type de retour async.


# Corps de la fonction et eléments retournés

Les accolades {} sont utilisées pour le corps de la fonction. Le corps de la fonction est un ensemble d'instructions exécutées lors de l'appel de la fonction. Dans notre exemple, pour setCount nous avons 2 instructions : 
Attribuer la valeur de count à la valeur de n. 
Retourner la valeur actuelle de count. 

Motoko permet d'omettre le retour à la fin du corps de la fonction, car un bloc est toujours évalué jusqu'à sa dernière expression. Ce qui signifie que nous pourrions réécrire le code de la manière suivante et qu'il serait toujours valide :


public func setCount(n : Nat) : async Nat {
    count := n;
    count;
};

# Public vs Private

Jusqu'à présent, nous n'avons vu que des fonctions publiques. Cependant, dans Motoko, vous pouvez également définir des fonctions privées.

private func add(n : Nat, m : Nat) : Nat {
    return (n + m)
};

La fonction est maintenant marquée private, ce qui signifie qu'elle ne peut être utilisée que par l'acteur lui-même et ne peut pas être appelée directement par les utilisateurs ou les canisters externes. 

Habituellement, les fonctions privées sont utilisées comme aides dans d'autres fonctions, qui sont généralement définies comme publiques. Par exemple, nous pourrions écrire ce qui suit.

actor {
    var count : Nat = 0;

    private func add(n : Nat, m : Nat) : Nat {
        return (n + m)
    };

    public func addCount(n : Nat) : async Nat {
        let newCount = add(count,n);
        count := newCount;
        return count;
    };
}

Nous pouvons supprimer le mot-clé private, une déclaration de fonction est par défaut une fonction privée dans Motoko, sauf indication contraire (c'est-à-dire à moins qu'elle ne soit déclarée publique).
